;#################################################################
;#								 #
;#	Copyright 2006, 2009 Fidelity Information Services, Inc	 #
;#								 #
;#	This source code contains the intellectual property	 #
;#	of its copyright holder(s), and is made available	 #
;#	under a license.  If you do not know the terms of	 #
;# 	the license, please stop and do not read further.	 #
;#								 #
;#################################################################
	.title	op_fnget "$Get() function"

;	OP_FNGET implements the $Get() function.
;	Upon entry, r1 -> source mval, r0 -> target mval.
;	If the source mval is defined, it is copied to the target,
;	otherwise, the target mval is set to be a null string.

	mval$def

	$routine OP_FNGET, entry=OP_FNGET_CA, kind=null

	beq	r1, undefined		; a nonexistent source is undefined
	mv_if_notdefined (r1), undefined

; Copy the source mval to the target:
	mov	mval$m_aliascont, r24
	ldl	r22, mval$w_mvtype(r1)	; includes mval$b_exp and the unreferenced fnpcid
	ldl	r23, mval$l_strlen(r1)
	bic	r22, r24, r22		; don't allow propagation of alias container flag
	ldl	r24, mval$a_straddr(r1)
	stl	r22, mval$w_mvtype(r0)
	stl	r23, mval$l_strlen(r0)
	stl	r24, mval$a_straddr(r0)
	ldl	r22, mval$l_m0(r1)
	ldl	r23, mval$l_m1(r1)
	stl	r22, mval$l_m0(r0)
	stl	r23, mval$l_m1(r0)

	ret	r26

undefined:
; Set the target mval to a null string:
	mov	mval$m_str, r22
	clr	r23
	stl	r22, mval$w_mvtype(r0)	; also clears mval$b_exp
	stl	r23, mval$l_strlen(r0)

	ret	r26

	$end_routine

	.end
